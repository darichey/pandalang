use std::str::FromStr;
use crate::ast::BinOp;
use crate::ast::BinOpKind;
use crate::ast::Expr;
use crate::ast::Fun;
use crate::ast::Int;
use crate::ast::Let;
use crate::ast::Var;

grammar;

pub Expr: Box<Expr> = {
    #[precedence(level="0")]
    Int => Box::new(Expr::Int(Int { n: <> })),
    Name => Box::new(Expr::Var(Var { name: <>.to_string() })),
    "(" <ExprReset> ")",

    #[precedence(level="1")]
    #[assoc(side="left")]
    <left:Expr> "*" <right:Expr> => Box::new(Expr::BinOp(BinOp { left, right, kind: BinOpKind::Mul })),
    <left:Expr> "/" <right:Expr> => Box::new(Expr::BinOp(BinOp { left, right, kind: BinOpKind::Div })),

    #[precedence(level="2")]
    #[assoc(side="left")]
    <left:Expr> "+" <right:Expr> => Box::new(Expr::BinOp(BinOp { left, right, kind: BinOpKind::Add })),
    <left:Expr> "-" <right:Expr> => Box::new(Expr::BinOp(BinOp { left, right, kind: BinOpKind::Sub })),

    #[precedence(level="3")]
    #[assoc(side="right")]
    "let" <name:Name> "=" <value:Expr> "in" <body:Expr> => Box::new(Expr::Let(Let { name: name.to_string(), value, body })),

    #[precedence(level="4")]
    #[assoc(side="right")]
    "fun" <arg:Name> "->" <body:Expr> => Box::new(Expr::Fun(Fun { arg: arg.to_string(), body })),
};

// See https://github.com/lalrpop/lalrpop/issues/596
ExprReset = <Expr>;

Int: i64 = r"\-?[0-9]+" => i64::from_str(<>).unwrap();

Name: &'input str = r"[a-zA-Z]+'?";
